#!/usr/bin/env perl
# nes-diff -- compare debug output to nestest

use v5.10;
use warnings;
use strict;

use IO::All qw(io);

my $txt_output = shift || die "need an input file";
my $nestest_output = shift || "doc/nestest.log";

my @txt = io->file($txt_output)->chomp->getlines;
my @nestest = io->file($nestest_output)->chomp->getlines;

# ignore the cruft I output at the top of my log
while ($txt[0] !~ /^[0-9A-Fa-f]{4}/) {
	shift @txt;
}

my $i = 0;
while (($i < @txt) and ($i < @nestest)) {
	my $txt = $txt[$i];
	my @txt_fields = (split / +/, $txt)[0, -5, -4, -3, -2, -1];
	$txt = join ' ', @txt_fields;

	my $nes = $nestest[$i];
	$nes =~ s/ CYC:.*$//g;
	my @nes_fields = (split / +/, $nes)[0, -5, -4, -3, -2, -1];
	$nes = join ' ', @nes_fields;

	if ($txt ne $nes) {
		say "$i - $nes";
		say "$i + $txt";
		last;
	}

	$i += 1;
}
